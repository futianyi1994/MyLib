apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    defaultConfig {
        applicationId rootProject.ext.android["applicationId"]
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        multiDexEnabled true
        testInstrumentationRunner rootProject.ext.android["androidJUnitRunner"]
        consumerProguardFiles 'consumer-rules.pro'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        ndk {
            abiFilters "armeabi", "armeabi-v7a"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'VERSION_DATE', "\"${releaseTime()}\""
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'VERSION_DATE', "\"${releaseTime()}\""
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //自动追加版本号和版本名称
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                if (buildType.name == 'release') {
                    outputFileName = "MyLib" + "_v" + defaultConfig.versionName + "_" + releaseTime() + ".apk"
                }
            }
    }
}

//得到打包时间
def static releaseTime() {
    return new Date().format("yyyy-MM-dd")
}


preBuild {
    doLast {
        def imlFile = file("../.idea/modules/${project.name}/" + rootProject.name + ".${project.name}.iml")
        println('Change ' + imlFile + ' order')
        try {
            def parsedXml = (new XmlParser()).parse(imlFile)
            def jdkNode = parsedXml.component[1].orderEntry.find { it.'@type' == 'jdk' }
            parsedXml.component[1].remove(jdkNode)
            def sdkString = "Android API " + android.compileSdkVersion.substring("android-".length()) + " Platform"
            new Node(parsedXml.component[1], 'orderEntry', ['type': 'jdk', 'jdkName': sdkString, 'jdkType': 'Android SDK'])
            groovy.xml.XmlUtil.serialize(parsedXml, new FileOutputStream(imlFile))
        } catch (FileNotFoundException e) {
            // nop, iml not found
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs', exclude: ['framework.jar'])
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]

    implementation project(':mylib')
    implementation project(':utils')
    //implementation rootProject.ext.dependencies["mylib"]
    //compileOnly files('libs/framework.jar')

    implementation rootProject.ext.dependencies["design"]
    implementation rootProject.ext.dependencies["support-v4"]
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["recyclerview-v7"]
    implementation rootProject.ext.dependencies["constraint-layout"]
    //Smartrefreshlayout
    implementation rootProject.ext.dependencies["smartrefreshlayout"]
    implementation rootProject.ext.dependencies["smartrefresh-header-classics"]
    implementation rootProject.ext.dependencies["smartrefresh-footer-classics"]
    //Rxbinding
    implementation rootProject.ext.dependencies["rxbinding2"]
    //Rxrelay2
    implementation rootProject.ext.dependencies["rxrelay2"]
    //Banner
    implementation rootProject.ext.dependencies["banner"]
    //Glide
    implementation rootProject.ext.dependencies["glide"]
    //Lottie-Android
    implementation rootProject.ext.dependencies["lottie"]
    //Room
    implementation rootProject.ext.dependencies["room"]
    annotationProcessor rootProject.ext.dependencies["room-compiler"]
    //Butterknife
    annotationProcessor rootProject.ext.dependencies["butterknife-compiler"]
}